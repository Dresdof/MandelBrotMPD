body Mandelbrot

	# Import nécessaire au rendu final.
	import MPDWin;
	
	# Constructeur pour le type "Complex".
	proc complex(realPart, imaginaryPart) returns c
	{	
		c = new(ComplexRec);
		c^.realPart = realPart;
		c^.imaginaryPart = imaginaryPart;
	}

	# Assigne un tableau de Complex selon le plan désigné. 
	proc assignateComplexArray(width, height, beginning, span) returns ca
	{
		int pixelsNumber = width * height;
		ca = new ([pixelsNumber] Complex);
	
		for [i = 1 to pixelsNumber] 
		{
			int x = i % width;
			int y = i / width;
	
			ca^[i] = complex(beginning^.realPart + x * span^.realPart / (width + 1), 
				beginning^.imaginaryPart + y * span^.imaginaryPart / (height + 1));
		}
	}

	# Retourne la valeur absolue du Complex passé en argument.
	procedure absolute (Complex complex) returns real r
	{
		r = sqrt( complex^.realPart**2 + complex^.imaginaryPart**2);
	}

	# Calcul de l'ensemble de Maldebrot pour un nombre.
	procedure mandelbrotCalculationS (Complex complexArg) returns int i
	{
		Complex tmpComplex = complex(complexArg^.realPart, complexArg^.imaginaryPart);

		for[n = 0 to maxIter]
		{
			i = n;
			if(absolute(tmpComplex) >= 2.0) {return;}
			
			real realPart = tmpComplex^.realPart**2 - tmpComplex^.imaginaryPart**2 + complexArg^.realPart;
			tmpComplex^.imaginaryPart = (tmpComplex^.realPart * tmpComplex^.imaginaryPart) * 2 + complexArg^.imaginaryPart;
			tmpComplex^.realPart = realPart;
		}
	}
	
	# Calcul séquentiel de l'ensemble de Mandelbrot.
	proc mandelbrotSequential(complexArray) returns ia
	{
		int n = ub(complexArray^);
		
		ia = new([n] int);
		
		for [i = 1 to n] {
			ia^[i] = mandelbrotCalculationS(complexArray^[i]);
		}
	}
	
	# Calcul parallèle à granularité fine
	proc mandelbrotPIF(complexArray) returns ia
	{
		int n = ub(complexArray^);
		
		ia = new([n] int);
		
		co [i = 1 to n]
			ia^[i] = mandelbrotCalculationS(complexArray^[i]);
		oc
	}

	# Fonction utilisée pour la récupération de la valeur de MPD_PARALLEL
	external getenv(string[*]) returns string[2];
	
	# Récupère la valeur de MPD_PARALLEL
	procedure getProcNumber() returns int procNumber
	{
		procNumber = 1;
		if (getenv("MPD_PARALLEL") != "") {
			procNumber = int(getenv("MPD_PARALLEL"));
		}
	}

	# Calcule la borne inférieure d'un morceau pour la version parallèle grossière adjacente.
	procedure getLowerBound(int iterator, int n, int procNumber) returns int i
	{
		i = (iterator - 1) * (n / procNumber) + min(n % procNumber, iterator-1) + 1;
	}
	
	# Calcule la borne supérieure d'un morceau pour la version parallèle grossière adjacente.
	procedure getUpperBound(int iterator, int n, int procNumber) returns int i
	{
		i = getLowerBound(iterator, n, procNumber) + n / procNumber + int(iterator <= n % procNumber) - 1;
	}

	# Calcul de chaque nombre d'un morceau en granularité grossière adjacente.
	procedure mandelbrotCalculationPIGA (ComplexArray complexArray, ref IntegerArray integerArray, int iterator)
	{
		int procNumber = getProcNumber();
		int n = ub(complexArray^);

		int lowerBound = getLowerBound(iterator, n, procNumber);
		int upperBound = getUpperBound(iterator, n, procNumber);

		for [ i = lowerBound to upperBound ]
		{
			integerArray^[i] = mandelbrotCalculationS(complexArray^[i]);
		}
	}
	
	# Calcul parallèle à granularité grossière adjacente.
	proc mandelbrotPIGA(complexArray) returns ia
	{
		int procNumber = getProcNumber();		
		ia = new([ub(complexArray^)] int);

		co [i = 1 to getProcNumber() ]
			mandelbrotCalculationPIGA(complexArray, ia, i);
		oc
	}

	# Calcul de chaque nombre d'un morceau en granularité grossière cyclique.
	procedure mandelbrotCalculationPIGC (ComplexArray complexArray, ref IntegerArray integerArray, int iterator)
	{
		int procNumber = getProcNumber();
		int n = ub(complexArray^);

		for [ i = iterator + 1 to n by procNumber ]
		{
			integerArray^[i] = mandelbrotCalculationS(complexArray^[i]);
		}
	}

	# Calcul parallèle à granularité grossière cyclique.
	proc mandelbrotPIGC(complexArray) returns ia
	{
		int procNumber = getProcNumber();		
		ia = new([ub(complexArray^)] int);

		co [i = 0 to getProcNumber() - 1 ]
			mandelbrotCalculationPIGC(complexArray, ia, i);
		oc
	}

	# Mutex utilisé pour le sac de tâches.
	sem mutex = 1;
	
	# Variables utilisées pour représenter le sac de tâches.
	int nextTask, taskSize, max;

	# Retourne la borne inférieure de la tâche suivante et incrémente le compteur (thread safe).
	procedure fa() returns int i
	{
		P(mutex);
		i = nextTask;
		nextTask += taskSize;
		V(mutex);
	}

	# Calcule les bornes de la tâche à obtenir.
	procedure getTask(res int lowerBound, res int upperBound) returns bool available
	{
		lowerBound = fa();
		upperBound = min(lowerBound + taskSize - 1, max);

		available = (lowerBound <= max);
	}

	# Calcul de chaque nombre d'une tâche. 
	procedure mandelbrotCalculationPST(ComplexArray complexArray, IntegerArray integerArray, int lowerBound, int upperBound)
	{
		for [ i = lowerBound to upperBound ]
		{
			integerArray^[i] = mandelbrotCalculationS(complexArray^[i]);
		}
	}

	# Exécute les tâches tant que le sac n'est pas vide.
	procedure executeTask(ComplexArray complexArray, IntegerArray integerArray)
	{
		int lowerBound, upperBound;
		
		while (getTask(lowerBound, upperBound))
		{
			mandelbrotCalculationPST(complexArray, integerArray, lowerBound, upperBound);	
		}
	}

	# Calcul parallèle à sac de tâches.
	proc mandelbrotPST(complexArray) returns ia
	{
		nextTask = 1;
		taskSize = 250;
		max = ub(complexArray^);

		ia = new([max] int);

		co [i = 1 to getProcNumber()]
			executeTask(complexArray, ia);
		oc
	} 

	# Dessin de la fractale à l'écran.
	proc draw (iterations, screen, width) 
	{
		int y = 0;
		int oldN = 0;
	
		for [i = 1 to ub(iterations^)] {	

			winPoint pt;
			
			pt.x = i % width;
			pt.y = y;
			
			int n = iterations^[i];
			
			if (n == maxIter) { n = 0; }
			
			if(n != oldN and n > 0) {
				if(n < 8)
				{
					if(n == 1) { WinSetForeground(screen, "grey"); }
					else if(n==2) { WinSetForeground(screen, "blue"); }
					else if(n==3) { WinSetForeground(screen, "yellow"); }
					else if(n==4) { WinSetForeground(screen, "red"); }
					else if(n==5) { WinSetForeground(screen, "cyan"); }
					else if(n==6) { WinSetForeground(screen, "magenta"); }
					else if(n>=7) { WinSetForeground(screen, "black"); }
				}
				else
				{
					if (oldN < 8) { WinSetForeground(screen, "black"); }
				}
			}

			if(n > 0) {
				WinDrawPixel(screen, pt);
			}
			
			oldN = n;
			if(pt.x + 1 == width) { y++; }
		}
	}
end
